plugins {
	id 'fabric-loom' version '0.2.6-SNAPSHOT'
	id 'com.github.johnrengelman.shadow' version '5.2.0'
	id 'maven-publish'
	id 'checkstyle'
	id 'net.minecrell.licenser' version '0.4.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

license {
	header = file('HEADER.txt')
	include '**/*.java'
	exclude '**/ScrollableContainer.java'
	exclude '**/ScrollableScreen.java'
	exclude '**/SearchTextFieldWidget.java'
	style.java = 'BLOCK_COMMENT'

	matching(includes: ['**/ScrollableContainer.java', '**/ScrollableScreen.java', '**/SearchTextFieldWidget.java']) {
		header = file('LICENSE-NINJAPHENIX.txt')
		style.java = 'BLOCK_COMMENT'
	}
}

repositories {
	mavenCentral()
	maven { url = "https://maven.fabricmc.net/io/github/prospector/modmenu/" }
	maven { url = "https://minecraft.curseforge.com/api/maven/" }
	maven { url = "https://maven.misterpemodder.com/libs-release/" }
	maven { url = "http://maven.sargunv.s3-website-us-west-2.amazonaws.com/" }
	maven { url = "http://server.bbkr.space:8081/artifactory/libs-release/" }
	maven { url = "https://repo.spongepowered.org/maven" }
	maven { url = "http://server.bbkr.space:8081/artifactory/libs-release" }
	maven { url = "https://www.jitpack.io" }
	maven { url   "https://dl.bintray.com/kyrptonaught/Quickshulker/"}
}

logger.lifecycle("""
	=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
	Building Cursed Shulker Boxes   

	Version: ${project.mod_version}
	Minecraft Version: ${project.minecraft_version}
	Fabric-Loader Version: ${project.loader_version}
	Fabric-API Version: ${project.fabric_version}
	Cotton-Resources Version: ${project.cotton_resources_version}
	LibCD Version: ${project.libcd_version}
	
	Output files will be in /build/libs
	=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
""")

minecraft {
}

dependencies {
	// Fabric Stuff.
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+${project.yarn_build}:v2"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
	modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "com.misterpemodder:shulkerboxtooltip:${project.shulkerboxtooltip_version}"

	//QuickShulker support
	modImplementation "net.kyrptonaught:quickshulker:${project.quickshulker_version}"

	// Cotton Resources -- Uncomment when 1.15 is pushed, this should be modRuntime due to LibCD handling the tags.
	// modImplementation ("io.github.cottonmc:cotton-resources:${cotton_resources_version}") { // We use our own version of modmenu and REI
	// exclude module: "fabric-api"
	// exclude module: "RoughlyEnoughItems"
	// }
	//include "io.github.cottonmc:cotton-resources:${cotton_resources_version}" // JIJ time

	// Optional Mods - We have this for looking up items and verifying recipes.
	modCompileOnly ("me.shedaniel:RoughlyEnoughItems:${project.rei_version}") {
		exclude(group : "net.fabricmc.fabric-api")
	}

	modImplementation "com.github.emilyploszaj:item-inventory:v1.1.0"
	include "com.github.emilyploszaj:item-inventory:v1.1.0"

	// LibCD is how we only load the CottonResources recipes IF CottonResources is present. We only use it at runtime and we also JIJ it out of the box.
	modImplementation ("io.github.cottonmc:LibCD:${project.libcd_version}") {
		exclude(group : "net.fabricmc.fabric-api")
		exclude module: "modmenu"
	}
	include ("io.github.cottonmc:LibCD:${project.libcd_version}") {}

	// Config
	implementation "org.spongepowered:configurate-hocon:${project.configurate_version}"
	include "org.spongepowered:configurate-core:${project.configurate_version}"
	include "org.spongepowered:configurate-hocon:${project.configurate_version}"
	include "com.typesafe:config:1.3.1"

	// Annotations
	implementation group: 'org.jetbrains', name: 'annotations', version: "${project.jetbrains_annotations}"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

checkstyle {
	configFile = rootProject.file("checkstyle.xml")
	toolVersion = '8.25'
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
